message         =   (fields / obs-fields)
                    [line-feeds body]

;; This next line is not part of the spec, but lets us parse email files
;; that have been been saved to a file system with UNIX line endings.

line-feeds      =   CRLF / LF

quoted-pair     =   ("\" (VCHAR / WSP)) / obs-qp

FWS             =   ([*WSP line-feeds] 1*WSP) /  obs-FWS
                                       ; Folding white space

ctext           =   %d33-39 /          ; Printable US-ASCII
                    %d42-91 /          ;  characters not including
                    %d93-126 /         ;  "(", ")", or "\"
                    obs-ctext

ccontent        =   ctext / quoted-pair / comment

comment         =   "(" *([FWS] ccontent) [FWS] ")"

CFWS            =   (1*([FWS] comment) [FWS]) / FWS

atext           =   ALPHA / DIGIT /    ; Printable US-ASCII
                    "!" / "#" /        ;  characters not including
                    "$" / "%" /        ;  specials.  Used for atoms.
                    "&" / "'" /
                    "*" / "+" /
                    "-" / "/" /
                    "=" / "?" /
                    "^" / "_" /
                    "`" / "{" /
                    "|" / "}" /
                    "~"

atom            =   [CFWS] 1*atext [CFWS]

dot-atom-text   =   1*atext *("." 1*atext)

dot-atom        =   [CFWS] dot-atom-text [CFWS]

specials        =   "(" / ")" /        ; Special characters that do
                    "<" / ">" /        ;  not appear in atext
                    "[" / "]" /
                    ":" / ";" /
                    "@" / "\" /
                    "," / "." /
                    DQUOTE

qtext           =   %d33 /             ; Printable US-ASCII
                       %d35-91 /          ;  characters not including
                       %d93-126 /         ;  "\" or the quote character
                       obs-qtext

qcontent        =   qtext / quoted-pair

quoted-string   =   [CFWS]
                    DQUOTE *([FWS] qcontent) [FWS] DQUOTE
                    [CFWS]

word            =   atom / quoted-string

phrase          =   1*word / obs-phrase

unstructured    =   (*([FWS] VCHAR) *WSP) / obs-unstruct

date-time       =   [ day-of-week "," ] date time [CFWS]

day-of-week     =   ([FWS] day-name) / obs-day-of-week

day-name        =   "Mon" / "Tue" / "Wed" / "Thu" /
                    "Fri" / "Sat" / "Sun"

date            =   day month year

day             =   ([FWS] 1*2DIGIT FWS) / obs-day

month           =   "Jan" / "Feb" / "Mar" / "Apr" /
                    "May" / "Jun" / "Jul" / "Aug" /
                    "Sep" / "Oct" / "Nov" / "Dec"

year            =   (FWS 4*DIGIT FWS) / obs-year

time            =   time-of-day zone

time-of-day     =   hour ":" minute [ ":" second ]

hour            =   2DIGIT / obs-hour

minute          =   2DIGIT / obs-minute

second          =   2DIGIT / obs-second

zone            =   (FWS ( "+" / "-" ) 4DIGIT) / obs-zone

address         =   mailbox / group

mailbox         =   name-addr / addr-spec

name-addr       =   [display-name] angle-addr

angle-addr      =   [CFWS] "<" addr-spec ">" [CFWS] /
                    obs-angle-addr

group           =   display-name ":" [group-list] ";" [CFWS]

display-name    =   phrase

mailbox-list    =   (mailbox *("," mailbox)) / obs-mbox-list

address-list    =   (address *("," address)) / obs-addr-list

group-list      =   mailbox-list / CFWS / obs-group-list

addr-spec       =   local-part "@" domain

local-part      =   dot-atom / quoted-string / obs-local-part

domain          =   dot-atom / domain-literal / obs-domain

domain-literal  =   [CFWS] "[" *([FWS] dtext) [FWS] "]" [CFWS]

dtext           =   %d33-90 /          ; Printable US-ASCII
                    %d94-126 /         ;  characters not including
                    obs-dtext          ;  "[", "]", or "\"

body            =   (*(*998text line-feeds) *998text) / obs-body

text            =   %d1-9 /            ; Characters excluding CR
                    %d11 /             ;  and LF
                    %d12 /
                    %d14-127

fields          =   *(trace
                      *optional-field /
                      *(resent-date /
                       resent-from /
                       resent-sender /
                       resent-to /
                       resent-cc /
                       resent-bcc /
                       resent-msg-id))
                    *(orig-date /
                    from /
                    sender /
                    reply-to /
                    to /
                    cc /
                    bcc /
                    message-id /
                    in-reply-to /
                    references /
                    subject /
                    comments /
                    keywords /
                    optional-field)

orig-date       =   "Date:" date-time line-feeds

from            =   "From:" mailbox-list line-feeds

sender          =   "Sender:" mailbox line-feeds

reply-to        =   "Reply-To:" address-list line-feeds

to              =   "To:" address-list line-feeds

cc              =   "Cc:" address-list line-feeds

bcc             =   "Bcc:" [address-list / CFWS] line-feeds

message-id      =   "Message-ID:" msg-id line-feeds

in-reply-to     =   "In-Reply-To:" 1*msg-id line-feeds

references      =   "References:" 1*msg-id line-feeds

msg-id          =   [CFWS] "<" id-left "@" id-right ">" [CFWS]

id-left         =   dot-atom-text / obs-id-left

id-right        =   dot-atom-text / no-fold-literal / obs-id-right

no-fold-literal =   "[" *dtext "]"

subject         =   "Subject:" unstructured line-feeds

comments        =   "Comments:" unstructured line-feeds

keywords        =   "Keywords:" phrase *("," phrase) line-feeds

resent-date     =   "Resent-Date:" date-time line-feeds

resent-from     =   "Resent-From:" mailbox-list line-feeds

resent-sender   =   "Resent-Sender:" mailbox line-feeds

resent-to       =   "Resent-To:" address-list line-feeds

resent-cc       =   "Resent-Cc:" address-list line-feeds

resent-bcc      =   "Resent-Bcc:" [address-list / CFWS] line-feeds

resent-msg-id   =   "Resent-Message-ID:" msg-id line-feeds

trace           =   [return]
                    1*received

return          =   "Return-Path:" path line-feeds

path            =   angle-addr / ([CFWS] "<" [CFWS] ">" [CFWS])

received        =   "Received:" *received-token ";" date-time line-feeds

received-token  =   word / angle-addr / addr-spec / domain

optional-field  =   field-name ":" unstructured line-feeds

field-name      =   1*ftext

ftext           =   %d33-57 /          ; Printable US-ASCII
                    %d59-126           ;  characters not including
                                       ;  ":".

obs-NO-WS-CTL   =   %d1-8 /            ; US-ASCII control
                    %d11 /             ;  characters that do not
                    %d12 /             ;  include the carriage
                    %d14-31 /          ;  return, line feed, and
                    %d127              ;  white space characters

obs-ctext       =   obs-NO-WS-CTL

obs-qtext       =   obs-NO-WS-CTL

obs-utext       =   %d0 / obs-NO-WS-CTL / VCHAR

obs-qp          =   "\" (%d0 / obs-NO-WS-CTL / LF / CR)

obs-body        =   *((*LF *CR *((%d0 / text) *LF *CR)) / line-feeds)

obs-unstruct    =   *((*LF *CR *(obs-utext *LF *CR)) / FWS)

obs-phrase      =   word *(word / "." / CFWS)

obs-phrase-list =   [phrase / CFWS] *("," [phrase / CFWS])

obs-FWS         =   1*WSP *(line-feeds 1*WSP)

obs-day-of-week =   [CFWS] day-name [CFWS]

obs-day         =   [CFWS] 1*2DIGIT [CFWS]

obs-year        =   [CFWS] 2*DIGIT [CFWS]

obs-hour        =   [CFWS] 2DIGIT [CFWS]

obs-minute      =   [CFWS] 2DIGIT [CFWS]

obs-second      =   [CFWS] 2DIGIT [CFWS]

obs-zone        =   "UT" / "GMT" /     ; Universal Time
                                       ; North American UT
                                       ; offsets
                    "EST" / "EDT" /    ; Eastern:  - 5/ - 4
                    "CST" / "CDT" /    ; Central:  - 6/ - 5
                    "MST" / "MDT" /    ; Mountain: - 7/ - 6
                    "PST" / "PDT" /    ; Pacific:  - 8/ - 7
                                       ;
                    %d65-73 /          ; Military zones - "A"
                    %d75-90 /          ; through "I" and "K"
                    %d97-105 /         ; through "Z", both
                    %d107-122          ; upper and lower case

obs-angle-addr  =   [CFWS] "<" obs-route addr-spec ">" [CFWS]

obs-route       =   obs-domain-list ":"

obs-domain-list =   *(CFWS / ",") "@" domain
                    *("," [CFWS] ["@" domain])

obs-mbox-list   =   *([CFWS] ",") mailbox *("," [mailbox / CFWS])

obs-addr-list   =   *([CFWS] ",") address *("," [address / CFWS])

obs-group-list  =   1*([CFWS] ",") [CFWS]

obs-local-part  =   word *("." word)

obs-domain      =   atom *("." atom)

obs-dtext       =   obs-NO-WS-CTL / quoted-pair

obs-fields      =   *(obs-return /
                    obs-received /
                    obs-orig-date /
                    obs-from /
                    obs-sender /
                    obs-reply-to /
                    obs-to /
                    obs-cc /
                    obs-bcc /
                    obs-message-id /
                    obs-in-reply-to /
                    obs-references /
                    obs-subject /
                    obs-comments /
                    obs-keywords /
                    obs-resent-date /
                    obs-resent-from /
                    obs-resent-send /
                    obs-resent-rply /
                    obs-resent-to /
                    obs-resent-cc /
                    obs-resent-bcc /
                    obs-resent-mid /
                    obs-optional)

obs-orig-date   =   "Date" *WSP ":" date-time line-feeds

obs-from        =   "From" *WSP ":" mailbox-list line-feeds

obs-sender      =   "Sender" *WSP ":" mailbox line-feeds

obs-reply-to    =   "Reply-To" *WSP ":" address-list line-feeds

obs-to          =   "To" *WSP ":" address-list line-feeds

obs-cc          =   "Cc" *WSP ":" address-list line-feeds

obs-bcc         =   "Bcc" *WSP ":"
                    (address-list / (*([CFWS] ",") [CFWS])) line-feeds

obs-message-id  =   "Message-ID" *WSP ":" msg-id line-feeds

obs-in-reply-to =   "In-Reply-To" *WSP ":" *(phrase / msg-id) line-feeds

obs-references  =   "References" *WSP ":" *(phrase / msg-id) line-feeds

obs-id-left     =   local-part

obs-id-right    =   domain

obs-subject     =   "Subject" *WSP ":" unstructured line-feeds

obs-comments    =   "Comments" *WSP ":" unstructured line-feeds

obs-keywords    =   "Keywords" *WSP ":" obs-phrase-list line-feeds

obs-resent-from =   "Resent-From" *WSP ":" mailbox-list line-feeds

obs-resent-send =   "Resent-Sender" *WSP ":" mailbox line-feeds

obs-resent-date =   "Resent-Date" *WSP ":" date-time line-feeds

obs-resent-to   =   "Resent-To" *WSP ":" address-list line-feeds

obs-resent-cc   =   "Resent-Cc" *WSP ":" address-list line-feeds

obs-resent-bcc  =   "Resent-Bcc" *WSP ":"
                    (address-list / (*([CFWS] ",") [CFWS])) line-feeds

obs-resent-mid  =   "Resent-Message-ID" *WSP ":" msg-id line-feeds

obs-resent-rply =   "Resent-Reply-To" *WSP ":" address-list line-feeds

obs-return      =   "Return-Path" *WSP ":" path line-feeds

obs-received    =   "Received" *WSP ":" *received-token line-feeds

obs-optional    =   field-name *WSP ":" unstructured line-feeds
