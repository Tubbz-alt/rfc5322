message         =   fields [line-feeds body]

;; This next line is not part of the spec, but lets us parse email files
;; that have been been saved to a file system with UNIX line endings.

line-feeds      =   CRLF / LF

quoted-pair     =   "\" (VCHAR / WSP)

FWS             =   [*WSP line-feeds] 1*WSP
                                       ; Folding white space

ctext           =   %d33-39 /          ; Printable US-ASCII
                    %d42-91 /          ;  characters not including
                    %d93-126           ;  "(", ")", or "\"

ccontent        =   ctext / quoted-pair / comment

comment         =   "(" *([FWS] ccontent) [FWS] ")"

CFWS            =   (1*([FWS] comment) [FWS]) / FWS

atext           =   ALPHA / DIGIT /    ; Printable US-ASCII
                    "!" / "#" /        ;  characters not including
                    "$" / "%" /        ;  specials.  Used for atoms.
                    "&" / "'" /
                    "*" / "+" /
                    "-" / "/" /
                    "=" / "?" /
                    "^" / "_" /
                    "`" / "{" /
                    "|" / "}" /
                    "~"

atom            =   [CFWS] 1*atext [CFWS]

dot-atom-text   =   1*atext *("." 1*atext)

dot-atom        =   [CFWS] dot-atom-text [CFWS]

specials        =   "(" / ")" /        ; Special characters that do
                    "<" / ">" /        ;  not appear in atext
                    "[" / "]" /
                    ":" / ";" /
                    "@" / "\" /
                    "," / "." /
                    DQUOTE

qtext           =   %d33 /             ; Printable US-ASCII
                    %d35-91 /          ;  characters not including
                    %d93-126           ;  "\" or the quote character

qcontent        =   qtext / quoted-pair

quoted-string   =   [CFWS]
                    DQUOTE *([FWS] qcontent) [FWS] DQUOTE
                    [CFWS]

word            =   atom / quoted-string

phrase          =   1*word

unstructured    =   *([FWS] text) *WSP

date-time       =   [ day-of-week "," ] date time [CFWS]

day-of-week     =   [FWS] day-name

day-name        =   "Mon" / "Tue" / "Wed" / "Thu" /
                    "Fri" / "Sat" / "Sun"

date            =   day month year

day             =   [FWS] 1*2DIGIT FWS

month           =   "Jan" / "Feb" / "Mar" / "Apr" /
                    "May" / "Jun" / "Jul" / "Aug" /
                    "Sep" / "Oct" / "Nov" / "Dec"

year            =   FWS 4*DIGIT FWS

time            =   time-of-day zone

time-of-day     =   hour ":" minute [ ":" second ]

hour            =   2DIGIT

minute          =   2DIGIT

second          =   2DIGIT

zone            =   FWS ( "+" / "-" ) 4DIGIT

address         =   mailbox / group

mailbox         =   name-addr / addr-spec

name-addr       =   [display-name] angle-addr

angle-addr      =   [CFWS] "<" addr-spec ">" [CFWS]

group           =   display-name ":" [group-list] ";" [CFWS]

display-name    =   phrase

mailbox-list    =   mailbox *("," mailbox)

address-list    =   address *("," address)

group-list      =   mailbox-list / CFWS

addr-spec       =   local-part "@" domain

local-part      =   dot-atom / quoted-string

domain          =   dot-atom / domain-literal

domain-literal  =   [CFWS] "[" *([FWS] dtext) [FWS] "]" [CFWS]

dtext           =   %d33-90 /          ; Printable US-ASCII
                    %d94-126           ;  characters not including
                                       ;  "[", "]", or "\"

body            =   *(*998text line-feeds) *998text

text            =   %d1-9 /            ; Characters excluding CR
                    %d11 /             ;  and LF
                    %d12 /
                    %d14-1114112

fields          =   *(trace
                      *optional-field /
                      *(resent-date /
                       resent-from /
                       resent-sender /
                       resent-to /
                       resent-cc /
                       resent-bcc /
                       resent-msg-id))
                    *(orig-date /
                    from /
                    sender /
                    reply-to /
                    to /
                    cc /
                    bcc /
                    message-id /
                    in-reply-to /
                    references /
                    subject /
                    comments /
                    keywords /
                    optional-field)

orig-date       =   "Date:" date-time line-feeds

from            =   "From:" mailbox-list line-feeds

sender          =   "Sender:" mailbox line-feeds

reply-to        =   "Reply-To:" address-list line-feeds

to              =   "To:" address-list line-feeds

cc              =   "Cc:" address-list line-feeds

bcc             =   "Bcc:" [address-list / CFWS] line-feeds

message-id      =   "Message-ID:" msg-id line-feeds

in-reply-to     =   "In-Reply-To:" 1*msg-id line-feeds

references      =   "References:" 1*msg-id line-feeds

msg-id          =   [CFWS] "<" id-left "@" id-right ">" [CFWS]

id-left         =   dot-atom-text

id-right        =   dot-atom-text / no-fold-literal

no-fold-literal =   "[" *dtext "]"

subject         =   "Subject:" unstructured line-feeds

comments        =   "Comments:" unstructured line-feeds

keywords        =   "Keywords:" phrase *("," phrase) line-feeds

resent-date     =   "Resent-Date:" date-time line-feeds

resent-from     =   "Resent-From:" mailbox-list line-feeds

resent-sender   =   "Resent-Sender:" mailbox line-feeds

resent-to       =   "Resent-To:" address-list line-feeds

resent-cc       =   "Resent-Cc:" address-list line-feeds

resent-bcc      =   "Resent-Bcc:" [address-list / CFWS] line-feeds

resent-msg-id   =   "Resent-Message-ID:" msg-id line-feeds

trace           =   [return]
                    1*received

return          =   "Return-Path:" path line-feeds

path            =   angle-addr / ([CFWS] "<" [CFWS] ">" [CFWS])

received        =   "Received:" *received-token ";" date-time line-feeds

received-token  =   word / angle-addr / addr-spec / domain

optional-field  =   field-name ":" unstructured line-feeds

field-name      =   1*ftext

ftext           =   %d33-57 /          ; Printable US-ASCII
                    %d59-126           ;  characters not including
                                       ;  ":".
